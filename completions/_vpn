#compdef vpn
# vim:ft=zsh:

local curcontext="${curcontext}"
local line
local state

local commands=(
    'down:Tear down a VPN connection'
    'edit:Edit a VPN definition'
    'show:Show VPN definition'
    'status:Show connected VPNs'
    'up:Establish a VPN connection'
)

# List inactive VPN connections.
function _vpn_list_inactive {
    # Get a list of active connections, so that we can exclude them from the
    # inactive list.
    local active="$(_vpn_list_active)"

    if [ -n "${active}" ]; then
        # We found active connections, remove them from the list.
        _vpn_list_all \
            | grep -v "${active}"
    else
        # No active connections, just show the whole list.
        _vpn_list_all
    fi
}

# List active VPN connections.
function _vpn_list_active {
    command nmcli connection show --active \
        | awk '$3 == "vpn" { print $1 }'
}

# List all VPN connections
function _vpn_list_all {
    command nmcli connection show \
        | awk '$3 == "vpn" { print $1 }'
}

_arguments -C \
    "1: :{_describe 'command' commands}" \
    "*:: :->vpn"

case $state in
    vpn)
        case "${line[1]}" in
            down)
                compadd "$@" "${(uonzf)$(_vpn_list_active)}"
                ;;
            edit)
                compadd "$@" "${(uonzf)$(_vpn_list_all)}"
                ;;
            show)
                compadd "$@" "${(uonzf)$(_vpn_list_all)}"
                ;;
            up)
                compadd "$@" "${(uonzf)$(_vpn_list_inactive)}"
                ;;
        esac
esac
